plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    alias libs.plugins.kotlin.jvm
    alias libs.plugins.kotlin.plugin
    alias libs.plugins.modDevGradle
}

kotlin {
    jvmToolchain(17)
}

group = maven_group
version = "${mod_version}"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

sourceSets {
    // Include resources generated by data generators.
    main.resources {
        srcDir 'src/generated/resources'
    }

    test {
        kotlin {
            srcDirs += 'src/test/java'
        }
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }

    extra {
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }
}

obfuscation {
    createRemappingConfiguration(configurations.extraRuntimeOnly)
}

apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"


mixin {
    var refmap = add sourceSets.main, "mixins.${mod_id}.refmap.json"
    slimJar.from refmap
    config "${mod_id}.mixins.json"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:-removal"
}

tasks.named("processResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
