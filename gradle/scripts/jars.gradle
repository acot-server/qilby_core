tasks.register('slimJar', Jar) {
    archiveClassifier = "dev-slim"
    from sourceSets.main.output
}

obfuscation {
    reobfuscate(tasks.named('slimJar'), sourceSets.main) {
        archiveClassifier = "slim"
    }
}

// Add slim and sources jar to components.java
java {
    artifacts.archives(reobfSlimJar)
    withSourcesJar()
}

// Use delombok'd files for sources
sourcesJar {
    exclude {
        sourceSets.main.allSource.contains it.file
    }
}

jar.archiveClassifier = "dev"

base {
    archivesName = "${project.name}-${libs.versions.minecraft.get()}"
}

afterEvaluate {
    reobfJar.archiveClassifier = ""
    tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
        destinationDirectory = file('build/libs/')
        manifest.attributes([
                'Specification-Title': project.name,
                'Specification-Version': mod_version,
                'Specification-Vendor': 'Annwan & Lunafreyja',
                'Implementation-Title': base.archivesName,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Annwan & Lunafreyja',
        ])
    }
}
